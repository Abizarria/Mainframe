       IDENTIFICATION      DIVISION.
       PROGRAM-ID.         PRGDB2C.
       AUTHOR.             ADILSON BIZARRIA.
       INSTALLATION.       FUTURE SCHOOL.
       DATE-WRITTEN.       11/21/2022.
       DATE-COMPILED.
      *-------------------------------------*
      * DB2 - CADASTRO DE ALUNOS (CONSULTA) *
      *-------------------------------------*
       ENVIRONMENT         DIVISION.
       CONFIGURATION       SECTION.

       SOURCE-COMPUTER.    IBM-3090.
       OBJECT-COMPUTER.    IBM-3090.

       SPECIAL-NAMES.
                           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT        SECTION.

       FILE-CONTROL.

       DATA                DIVISION.
       FILE                SECTION.

       WORKING-STORAGE     SECTION.

       77  WS-TIME         PIC S9(15)  COMP-3.
       77  WS-DATA         PIC  X(08).
       77  WS-HORA         PIC  X(05).
       77  WS-FASE         PIC  9(01).
       77  WS-IMSG         PIC  9(01).

           COPY    DFHAID.
           COPY    DFHBMSCA.
           COPY    MAPDB2C.

           EXEC    SQL     INCLUDE    SQLCA     END-EXEC.
           EXEC    SQL     INCLUDE    DB2ALU    END-EXEC.

       LINKAGE             SECTION.

       01  DFHCOMMAREA.
           03  LK-FASE     PIC  9(01).

       PROCEDURE           DIVISION.

       000-INI-PROG.
           IF  EIBCALEN   =   0
               GO  TO         100-INI-MAPA
           ELSE
               GO  TO         200-REC-TELA
           END-IF.

       100-INI-MAPA.
           MOVE     0         TO   TMTRCO
           MOVE     SPACES    TO   TNOMEO    TCURSO    TTURMO
                                   TENDRO    TBAIRO    TCIDAO
                                   TESTDO    TMAILO    TFONEO

           MOVE    'INFORME O NUMERO DA MATRICULA' TO  TMSGMO

           MOVE     DFHBMFSE  TO   TMTRCA
           MOVE    -1         TO   TMTRCL
           MOVE     1         TO   WS-FASE.

       110-BLQ-ENTR.
           MOVE     DFHBMPRF  TO   TNOMEA    TCURSA    TTURMA
                                   TENDRA    TBAIRA    TCIDAA
                                   TESTDA    TMAILA    TFONEA.
       120-ATU-DATA.
           EXEC     CICS  ASKTIME
                          ABSTIME (WS-TIME)
           END-EXEC

           EXEC     CICS  FORMATTIME
                          ABSTIME (WS-TIME)
                          DATESEP ('/')
                          DDMMYY  (WS-DATA)
                          TIMESEP (':')
                          TIME    (WS-HORA)
           END-EXEC

           MOVE     WS-DATA   TO   TDATAO
           MOVE     WS-HORA   TO   THORAO
           MOVE     DFHBMPRF  TO   TDATAA    THORAA.

       130-ATU-TELA.
           MOVE    'CONSULTA' TO   TOPR1O
           MOVE     EIBTRNID  TO   TTRNSO
           MOVE     EIBTRMID  TO   TTERMO
           MOVE     SPACES    TO   TPCSRO

           MOVE     DFHBMPRF  TO   TOPR1A    TTRNSA
                                   TTERMA    TMSGMA    TPCSRA

           EXEC     CICS  SEND     MAP      ('MAPDB2C')
                                   MAPSET   ('MAPDB2C')
                          FROM              (MAPDB2CO)
                          ERASE    CURSOR
           END-EXEC

           EXEC     CICS  SEND     PAGE      END-EXEC

           EXEC     CICS  RETURN   COMMAREA (WS-FASE)
                                   TRANSID  ('DB2C')
           END-EXEC.

       200-REC-TELA.
           EXEC     CICS  RECEIVE MAP     ('MAPDB2C')
                                  MAPSET  ('MAPDB2C')
                          INTO            (MAPDB2CI)
           END-EXEC

           MOVE     LK-FASE   TO   WS-FASE

           IF  EIBAID    =    DFHPF3  AND  WS-FASE  >  0
               EXEC    CICS   XCTL
                              PROGRAM     ('PRGDB2M')
               END-EXEC
           END-IF

           EVALUATE WS-FASE
              WHEN  0
                    GO  TO    100-INI-MAPA
              WHEN  1
                    GO  TO    210-CHK-MTRC
           END-EVALUATE.

       210-CHK-MTRC.
           EXEC     CICS  BIF      DEEDIT
                          FIELD   (TMTRCI)
                          LENGTH  (5)
           END-EXEC

           IF  TMTRCI   NOT   NUMERIC  OR
               TMTRCI   =     '00000'
               MOVE     1     TO   WS-IMSG
               GO  TO   220-ATU-MSGM.

           MOVE     TMTRCI    TO   MAT-ALU

           EXEC     SQL   SELECT   MAT_ALU,
                                   NOM_ALU,  CUR_ALU,  TUR_ALU,
                                   END_ALU,  BAI_ALU,  CID_ALU,
                                   EST_ALU,  EML_ALU,  TEL_ALU

                          INTO    :MAT-ALU,
                                  :NOM-ALU, :CUR-ALU, :TUR-ALU,
                                  :END-ALU, :BAI-ALU, :CID-ALU,
                                  :EST-ALU, :EML-ALU, :TEL-ALU

                          FROM     DBADB2.TABALU

                          WHERE    MAT_ALU      =     :MAT-ALU
           END-EXEC

           IF  SQLCODE    =   0
               MOVE     NOM-ALU   TO   TNOMEO
               MOVE     CUR-ALU   TO   TCURSO
               MOVE     TUR-ALU   TO   TTURMO
               MOVE     END-ALU   TO   TENDRO
               MOVE     BAI-ALU   TO   TBAIRO
               MOVE     CID-ALU   TO   TCIDAO
               MOVE     EST-ALU   TO   TESTDO
               MOVE     EML-ALU   TO   TMAILO
               MOVE     TEL-ALU   TO   TFONEO

               MOVE     2         TO   WS-IMSG
           ELSE
               MOVE     3         TO   WS-IMSG
           END-IF.

       220-ATU-MSGM.
           IF  WS-IMSG  NOT  =   2
               EXEC     CICS  SEND     CONTROL   ALARM    END-EXEC.

           EVALUATE WS-IMSG
              WHEN  1
                    MOVE 'NUMERO DA MATRICULA INVALIDO!'  TO  TMSGMO
              WHEN  2
                    MOVE 'CONSULTA BEM SUCEDIDA!       '  TO  TMSGMO
              WHEN  3
                    MOVE 'MATRICULA NAO ENCONTRADA!    '  TO  TMSGMO
           END-EVALUATE

           MOVE     DFHBMPRF  TO   TMTRCA
           MOVE     -1        TO   TPCSRL
           MOVE      0        TO   WS-FASE
           GO  TO   110-BLQ-ENTR.
