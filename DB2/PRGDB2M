       IDENTIFICATION      DIVISION.
       PROGRAM-ID.         PRGDB2M.
       AUTHOR.             ADILSON BIZARRIA.
       INSTALLATION.       FUTURE SCHOOL.
       DATE-WRITTEN.       11/14/2022.
       DATE-COMPILED.
      *-------------------------------------------*
      * DB2 - CADASTRO DE ALUNOS (MENU PRINCIPAL) *
      *-------------------------------------------*
       ENVIRONMENT         DIVISION.
       CONFIGURATION       SECTION.

       SOURCE-COMPUTER.    IBM-3090.
       OBJECT-COMPUTER.    IBM-3090.

       SPECIAL-NAMES.
                           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT        SECTION.

       FILE-CONTROL.

       DATA                DIVISION.
       FILE                SECTION.

       WORKING-STORAGE     SECTION.

       77  WS-TIME         PIC S9(15)  COMP-3.
       77  WS-DATA         PIC  X(08).
       77  WS-HORA         PIC  X(05).
       77  WS-MSGM         PIC  X(20).
       77  WS-FASE         PIC  9(01).

           COPY    DFHAID.
           COPY    DFHBMSCA.
           COPY    MAPDB2M.

       LINKAGE             SECTION.

       01 DFHCOMMAREA.
          03  LK-FASE      PIC X(01).

       PROCEDURE           DIVISION.

       000-INI-PROG.
           IF  EIBCALEN    =   0
               GO  TO      100-INI-MAPA
           ELSE
               GO  TO      200-REC-TELA
           END-IF.

       100-INI-MAPA.
           MOVE     ZEROS     TO   WS-FASE.

       110-ATU-TELA.
           PERFORM  120-ATU-DATA

           MOVE     EIBTRMID  TO   TTERMO
           MOVE     EIBTRNID  TO   TTRNSO
           MOVE     DFHBMPRF  TO   TTRNSA   TTERMA

           EXEC     CICS  SEND     MAP      ('MAPDB2M')
                                   MAPSET   ('MAPDB2M')
                          FROM              (MAPDB2MO)
                          ERASE    CURSOR
           END-EXEC

           PERFORM  130-ATU-MSGM

           EXEC     CICS  SEND     PAGE     END-EXEC

           EXEC     CICS  RETURN   COMMAREA (WS-FASE)
                                   TRANSID  ('DB2M')
           END-EXEC.

       120-ATU-DATA.
           EXEC     CICS      ASKTIME
                              ABSTIME    (WS-TIME)
           END-EXEC

           EXEC     CICS      FORMATTIME
                              ABSTIME    (WS-TIME)
                              DATESEP    ('/')
                              DDMMYY     (WS-DATA)
                              TIMESEP    (':')
                              TIME       (WS-HORA)
           END-EXEC

           MOVE     WS-DATA   TO   TDATAO
           MOVE     WS-HORA   TO   THORAO
           MOVE     DFHBMPRF  TO   TDATAA   THORAA.

       130-ATU-MSGM.
           EVALUATE WS-FASE
             WHEN   0
                    MOVE     ZEROS     TO   TNOPCO
                    MOVE     DFHBMFSE  TO   TNOPCA
                    MOVE     -1        TO   TNOPCL

                    EXEC     CICS  SEND     MAP     ('MSGDB2M')
                                            MAPSET  ('MAPDB2M')
                                   FROM             (MSGDB2MO)
                                   CURSOR
                    END-EXEC
             WHEN   1
                    MOVE     SPACES    TO   TPCSRO
                    MOVE     DFHBMPRF  TO   TPCSRA
                    MOVE     -1        TO   TPCSRL

                    EXEC     CICS  SEND     MAP     ('ERRDB2M')
                                            MAPSET  ('MAPDB2M')
                                   FROM             (ERRDB2MO)
                                   CURSOR   ALARM
                    END-EXEC
           END-EVALUATE.

       200-REC-TELA.
           EXEC     CICS  RECEIVE  MAP      ('MSGDB2M')
                                   MAPSET   ('MAPDB2M')
                          INTO              (MSGDB2MI)
           END-EXEC

           IF  EIBAID     =   DFHPF3
               PERFORM        900-FIM-PROG.

           MOVE     LK-FASE   TO   WS-FASE

           IF  WS-FASE    =   1
               GO  TO         100-INI-MAPA.

           EVALUATE TNOPCI
             WHEN   1
                    EXEC     CICS  XCTL
                                   PROGRAM  ('PRGDB2I')
                    END-EXEC
             WHEN   2
                    EXEC     CICS  XCTL
                                   PROGRAM  ('PRGDB2A')
                    END-EXEC
             WHEN   3
                    EXEC     CICS  XCTL
                                   PROGRAM  ('PRGDB2C')
                    END-EXEC
             WHEN   4
                    EXEC     CICS  XCTL
                                   PROGRAM  ('PRGDB2E')
                    END-EXEC
             WHEN   5
                    EXEC     CICS  START
                                   TRANSID  ('DB2R)
                    END-EXEC
                           
                    EXEC     CICS  XCTL
                                   PROGRAM  ('PRGDB2R')
                    END-EXEC
             WHEN   6
                    PERFORM  900-FIM-PROG
             WHEN   OTHER
                    MOVE     1         TO    WS-FASE
                    PERFORM  110-ATU-TELA
           END-EVALUATE.

       900-FIM-PROG.
           MOVE    'PROGRAMA ENCERRADO' TO   WS-MSGM

           EXEC     CICS  SEND
                          FROM   (WS-MSGM)
                          ERASE
           END-EXEC

           EXEC     CICS  RETURN  END-EXEC.
