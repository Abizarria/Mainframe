       IDENTIFICATION      DIVISION.
       PROGRAM-ID.         PRGDB2I.
       AUTHOR.             ADILSON BIZARRIA.
       INSTALLATION.       FUTURE SCHOOL.
       DATE-WRITTEN.       11/16/2022.
       DATE-COMPILED.
      *-------------------------------------*
      * DB2 - CADASTRO DE ALUNOS (INCLUSAO) *
      *-------------------------------------*
       ENVIRONMENT         DIVISION.
       CONFIGURATION       SECTION.

       SOURCE-COMPUTER.    IBM-3090.
       OBJECT-COMPUTER.    IBM-3090.

       SPECIAL-NAMES.
                           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT        SECTION.

       FILE-CONTROL.

       DATA                DIVISION.
       FILE                SECTION.

       WORKING-STORAGE     SECTION.

       77  WS-TIME         PIC S9(15)  COMP-3.
       77  WS-DATA         PIC  X(08).
       77  WS-HORA         PIC  X(05).
       77  WS-FASE         PIC  9(01).
       77  WS-IMSG         PIC  9(01).

           COPY    DFHAID.
           COPY    DFHBMSCA.
           COPY    MAPDB2C.

           EXEC    SQL     INCLUDE    SQLCA     END-EXEC.
           EXEC    SQL     INCLUDE    DB2ALU    END-EXEC.

       LINKAGE             SECTION.

       01  DFHCOMMAREA.
           03  LK-FASE     PIC  9(01).

       PROCEDURE           DIVISION.

       000-INI-PROG.
           IF  EIBCALEN   =   0
               GO  TO         100-INI-MAPA
           ELSE
               GO  TO         200-REC-TELA
           END-IF.

       100-INI-MAPA.
           MOVE     0         TO   TMTRCO
           MOVE     SPACES    TO   TNOMEO    TCURSO    TTURMO
                                   TENDRO    TBAIRO    TCIDAO
                                   TESTDO    TMAILO    TFONEO

           MOVE    'INFORME O NUMERO DA MATRICULA' TO  TMSGMO

           MOVE     DFHBMFSE  TO   TMTRCA
           MOVE    -1         TO   TMTRCL
           MOVE     1         TO   WS-FASE.

       110-BLQ-ENTR.
           MOVE     DFHBMPRF  TO   TNOMEA    TCURSA    TTURMA
                                   TENDRA    TBAIRA    TCIDAA
                                   TESTDA    TMAILA    TFONEA.
       120-ATU-DATA.
           EXEC     CICS  ASKTIME
                          ABSTIME (WS-TIME)
           END-EXEC

           EXEC     CICS  FORMATTIME
                          ABSTIME (WS-TIME)
                          DATESEP ('/')
                          DDMMYY  (WS-DATA)
                          TIMESEP (':')
                          TIME    (WS-HORA)
           END-EXEC

           MOVE     WS-DATA   TO   TDATAO
           MOVE     WS-HORA   TO   THORAO
           MOVE     DFHBMPRF  TO   TDATAA    THORAA.

       130-ATU-TELA.
           MOVE    'INCLUSAO' TO   TOPR1O
           MOVE     EIBTRNID  TO   TTRNSO
           MOVE     EIBTRMID  TO   TTERMO
           MOVE     SPACES    TO   TPCSRO

           MOVE     DFHBMPRF  TO   TOPR1A    TTRNSA
                                   TTERMA    TMSGMA   TPCSRA

           EXEC     CICS  SEND     MAP      ('MAPDB2C')
                                   MAPSET   ('MAPDB2C')
                          FROM              (MAPDB2CO)
                          ERASE    CURSOR
           END-EXEC

           IF  WS-FASE    =   3
               PERFORM        140-MAPA-RSP.

           EXEC     CICS  SEND     PAGE      END-EXEC

           EXEC     CICS  RETURN   COMMAREA (WS-FASE)
                                   TRANSID  ('DB2I')
           END-EXEC.

       140-MAPA-RSP.
           MOVE    'INCLUSAO' TO   TOPR2O
           MOVE     SPACES    TO   TRESPO
           MOVE     DFHBMPRF  TO   TOPR2A
           MOVE     DFHBMFSE  TO   TRESPA
           MOVE     -1        TO   TRESPL

           EXEC     CICS  SEND     MAP      ('RSPDB2C')
                                   MAPSET   ('MAPDB2C')
                          FROM              (RSPDB2CO)
                          CURSOR
           END-EXEC.

       200-REC-TELA.
           EVALUATE WS-FASE
              WHEN  3
                    EXEC     CICS  RECEIVE MAP     ('RSPDB2C')
                                           MAPSET  ('MAPDB2C')
                                   INTO             (RSPDB2CI)
                    END-EXEC
              WHEN  OTHER
                    EXEC     CICS  RECEIVE MAP     ('MAPDB2C')
                                           MAPSET  ('MAPDB2C')
                                   INTO            (MAPDB2CI)
                    END-EXEC
           END-EVALUATE

           MOVE     LK-FASE   TO   WS-FASE

           IF  EIBAID    =    DFHPF3   AND  WS-FASE  >  0
               IF  WS-FASE    >    1
                   MOVE    0       TO  WS-FASE
               ELSE
                   EXEC    CICS    XCTL
                                   PROGRAM  ('PRGDB2M')
                   END-EXEC
               END-IF
           END-IF

           EVALUATE WS-FASE
              WHEN  0
                    GO  TO    100-INI-MAPA
              WHEN  1
                    GO  TO    300-CHK-CODE
              WHEN  2
                    GO  TO    310-CHK-ENTR
              WHEN  3
                    GO  TO    320-CHK-RESP
           END-EVALUATE.

       300-CHK-CODE.
           EXEC     CICS  BIF      DEEDIT
                          FIELD   (TMTRCI)
                          LENGTH  (5)
           END-EXEC

           IF  TMTRCI   NOT   NUMERIC  OR
               TMTRCI   =     '00000'
               MOVE     1     TO  WS-IMSG
               GO  TO   400-ATU-MSGM.

           MOVE     TMTRCI    TO  MAT-ALU

           EXEC     SQL   SELECT  MAT_ALU,
                                  NOM_ALU,  CUR_ALU,  TUR_ALU,
                                  END_ALU,  BAI_ALU,  CID_ALU,
                                  EST_ALU,  EML_ALU,  TEL_ALU

                          INTO   :MAT-ALU,
                                 :NOM-ALU, :CUR-ALU, :TUR-ALU,
                                 :END-ALU, :BAI-ALU, :CID-ALU,
                                 :EST-ALU, :EML-ALU, :TEL-ALU

                          FROM    DBADB2.TABALU

                          WHERE   MAT_ALU      =     :MAT-ALU
           END-EXEC

           IF  SQLCODE    =   0
               MOVE     NOM-ALU   TO   TNOMEO
               MOVE     CUR-ALU   TO   TCURSO
               MOVE     TUR-ALU   TO   TTURMO
               MOVE     END-ALU   TO   TENDRO
               MOVE     BAI-ALU   TO   TBAIRO
               MOVE     CID-ALU   TO   TCIDAO
               MOVE     EST-ALU   TO   TESTDO
               MOVE     EML-ALU   TO   TMAILO
               MOVE     TEL-ALU   TO   TFONEO

               MOVE     2         TO   WS-IMSG
               GO  TO   400-ATU-MSGM.

           MOVE     2         TO   WS-FASE.

       310-CHK-ENTR.
           MOVE     DFHBMPRF  TO   TMTRCA
           PERFORM  110-BLQ-ENTR

           EVALUATE TRUE
              WHEN  TNOMEI   =  LOW-VALUES  OR  SPACES
                    MOVE    'INFORME O NOME DO ALUNO    '  TO  TMSGMO
                    MOVE     DFHBMFSE  TO  TNOMEA
                    MOVE     -1        TO  TNOMEL
              WHEN  TCURSI   =  LOW-VALUES  OR  SPACES
                    MOVE    'INFORME O NOME DO CURSO    '  TO  TMSGMO
                    MOVE     DFHBMFSE  TO  TCURSA
                    MOVE     -1        TO  TCURSL
              WHEN  TTURMI   =  LOW-VALUES  OR  SPACES
                    MOVE    'INFORME A TURMA DO ALUNO   '  TO  TMSGMO
                    MOVE     DFHBMFSE  TO  TTURMA
                    MOVE     -1        TO  TTURML
              WHEN  TENDRI   =  LOW-VALUES  OR  SPACES
                    MOVE    'INFORME O ENDERECO DO ALUNO'  TO  TMSGMO
                    MOVE     DFHBMFSE  TO  TENDRA
                    MOVE     -1        TO  TENDRL
              WHEN  TBAIRI   =  LOW-VALUES  OR  SPACES
                    MOVE    'INFORME O NOME DO BAIRRO   '  TO  TMSGMO
                    MOVE     DFHBMFSE  TO  TBAIRA
                    MOVE     -1        TO  TBAIRL
              WHEN  TCIDAI   =  LOW-VALUES  OR  SPACES
                    MOVE    'INFORME O NOME DO CIDADE   '  TO  TMSGMO
                    MOVE     DFHBMFSE  TO  TCIDAA
                    MOVE     -1        TO  TCIDAL
              WHEN  TESTDI   =  LOW-VALUES  OR  SPACES
                    MOVE    'INFORME A SIGLA DO ESTADO  '  TO  TMSGMO
                    MOVE     DFHBMFSE  TO  TESTDA
                    MOVE     -1        TO  TESTDL
              WHEN  TMAILI   =  LOW-VALUES  OR  SPACES
                    MOVE    'INFORME O E-MAIL DO ALUNO  '  TO  TMSGMO
                    MOVE     DFHBMFSE  TO  TMAILA
                    MOVE     -1        TO  TMAILL
              WHEN  TFONEI   =  LOW-VALUES  OR  SPACES
                    MOVE    'INFORME O TELEFONE DO ALUNO'  TO  TMSGMO
                    MOVE     DFHBMFSE  TO  TFONEA
                    MOVE     -1        TO  TFONEL
              WHEN  OTHER
                    MOVE     3         TO  WS-FASE
           END-EVALUATE.

           GO  TO   120-ATU-DATA.

       320-CHK-RESP.
           EVALUATE TRESPI
              WHEN  'S'
                    GO  TO    240-GRV-MTRC
              WHEN  'N'
                    GO  TO    100-INI-MAPA
              WHEN  OTHER
                    MOVE      3   TO  WS-IMSG
                    MOVE      2   TO  WS-FASE
                    GO  TO    400-ATU-MSGM
           END-EVALUATE.

       240-GRV-MTRC.
           MOVE     TMTRCI    TO   MAT-ALU
           MOVE     TNOMEI    TO   NOM-ALU
           MOVE     TCURSI    TO   CUR-ALU
           MOVE     TTURMI    TO   TUR-ALU
           MOVE     TENDRI    TO   END-ALU
           MOVE     TBAIRI    TO   BAI-ALU
           MOVE     TCIDAI    TO   CID-ALU
           MOVE     TESTDI    TO   EST-ALU
           MOVE     TMAILI    TO   EML-ALU
           MOVE     TFONEI    TO   TEL-ALU

           EXEC     SQL   INSERT   INTO      DBADB2.TABALU
                                 ( MAT_ALU,
                                   NOM_ALU,  CUR_ALU,  TUR_ALU,
                                   END_ALU,  BAI_ALU,  CID_ALU,
                                   EST_ALU,  EML_ALU,  TEL_ALU )

                          VALUES (:MAT-ALU,
                                  :NOM-ALU, :CUR-ALU, :TUR-ALU,
                                  :END-ALU, :BAI-ALU, :CID-ALU,
                                  :EST-ALU, :EML-ALU, :TEL-ALU )
           END-EXEC

           MOVE     1         TO   WS-FASE

           IF  SQLCODE   =    0
               MOVE      4    TO   WS-IMSG
           ELSE
               MOVE      5    TO   WS-IMSG
           END-IF.

       400-ATU-MSGM.
           IF  WS-IMSG  NOT   =    4
               EXEC     CICS  SEND  CONTROL  ALARM  END-EXEC.

           EVALUATE WS-IMSG
              WHEN  1
                    MOVE 'NUMERO DA MATRICULA INVALIDO!'  TO  TMSGMO
              WHEN  2
                    MOVE 'MATRICULA JA CADASTRADA!     '  TO  TMSGMO
              WHEN  3
                    MOVE 'RESPONDA COM S OU N APENAS!  '  TO  TMSGMO
              WHEN  4
                    MOVE 'INCLUSAO BEM SUCEDIDA!       '  TO  TMSGMO
              WHEN  5
                    MOVE 'ERRO NA INCLUSAO DO ALUNO!   '  TO  TMSGMO
           END-EVALUATE

           MOVE     DFHBMPRF  TO   TMTRCA
           MOVE     -1        TO   TPCSRL

           IF  WS-FASE   =    1
               MOVE      0    TO   WS-FASE.
           GO  TO   110-BLQ-ENTR.
